Terminals:
    "LowerId" = Token(kind = TokenKind.LowerId | TokenKind.Self_, ..)
    "UpperId" = Token(kind = TokenKind.UpperId, ..)
    "type" = Token(kind = TokenKind.Type, ..)
    "prim" = Token(kind = TokenKind.Prim, ..)
    "Fn" = Token(kind = TokenKind.UpperFn, ..)
    "(" = Token(kind = TokenKind.LParen, ..)
    ")" = Token(kind = TokenKind.RParen, ..)
    "[" = Token(kind = TokenKind.LBracket, ..)
    "]" = Token(kind = TokenKind.RBracket, ..)
    ":" = Token(kind = TokenKind.Colon, ..)
    "," = Token(kind = TokenKind.Comma, ..)
    ".." = Token(kind = TokenKind.DotDot, ..)
    "NEWLINE" = Token(kind = TokenKind.Newline, ..)
    "INDENT" = Token(kind = TokenKind.Indent, ..)
    "DEDENT" = Token(kind = TokenKind.Dedent, ..)

type_:
    namedType
    "LowerId"
    recordType
    variantType
    fnType

recordType:
    _"(" _")"
    _"(" recordTypeFields _")"

variantType:
    _"[" _"]"
    _"[" variantAlt (_"," variantAlt)* _"," _".." "LowerId" _"]"
    _"[" variantAlt (_"," variantAlt)* _","? _"]"

fnType:
    _"Fn" _"(" fnArgs? _")" returnType?

namedType:
    "UpperId" _"[" type_ (_"," type_)* _","? _"]"
    "UpperId"

recordTypeFields:
    recordTypeField (_"," recordTypeField)* _"," _".." "LowerId"
    recordTypeField (_"," recordTypeField)* _","?

recordTypeField:
    "LowerId" _":" type_
    type_

variantAlt:
    "UpperId" _"(" recordTypeFields _")"
    "UpperId"

fnArgs:
    param (_"," param)* ","?

param:
    "LowerId" (_":" type_)?

returnType:
    _":" type_ type_?

# --------------------------------------------------------------------------------------------------

typeDecl:
    _"type" "UpperId" (_"[" typeParams _"]")? _":" _"NEWLINE" _"INDENT" typeDeclRhs _"DEDENT"
    _"type" "UpperId" (_"[" typeParams _"]")? _"NEWLINE"
    "prim" _"type" "UpperId" (_"[" typeParams _"]")? _"NEWLINE"

typeParams:
    "LowerId" (_"," "LowerId")* ","?
    "LowerId"

typeDeclRhs:
    conDecl+
    namedField+

conDecl:
    "UpperId" _":" _"NEWLINE" _"INDENT" namedField+ _"DEDENT" _"NEWLINE"
    "UpperId" _"(" unnamedFields? _")" _"NEWLINE"
    "UpperId" _"NEWLINE"

namedField:
    "LowerId" _":" type_ _"NEWLINE"

unnamedFields:
    type_ (_"," type_)* _","?
