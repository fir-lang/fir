#![allow(clippy::all)]

use crate::ast::*;
use crate::interpolation::parse_string_parts;
use crate::parser_utils::parse_char_lit;
use crate::token::*;

use std::convert::Infallible;
use std::rc::Rc;

use lexgen_util::{LexerError, Loc};

grammar<'a>(module: &'a Rc<str>);

extern {
    type Location = Loc;
    type Error = LexerError<Infallible>;

    enum Token {
        UpperId => Token { kind: TokenKind::UpperId, .. },
        BacktickUpperId => Token { kind: TokenKind::BacktickUpperId, .. },
        LowerId => Token { kind: TokenKind::LowerId, .. },
        "(" => Token { kind: TokenKind::LParen, .. },
        ")" => Token { kind: TokenKind::RParen, .. },
        "[" => Token { kind: TokenKind::LBracket, .. },
        "]" => Token { kind: TokenKind::RBracket, .. },
        "{" => Token { kind: TokenKind::LBrace, .. },
        "}" => Token { kind: TokenKind::RBrace, .. },
        ":" => Token { kind: TokenKind::Colon, .. },
        "," => Token { kind: TokenKind::Comma, .. },
        "." => Token { kind: TokenKind::Dot, .. },
        "=" => Token { kind: TokenKind::Eq, .. },
        ".." => Token { kind: TokenKind::DotDot, .. },
        "..=" => Token { kind: TokenKind::DotDotEq, .. },
        "_" => Token { kind: TokenKind::Underscore, .. },
        "==" => Token { kind: TokenKind::EqEq, .. },
        "+" => Token { kind: TokenKind::Plus, .. },
        "-" => Token { kind: TokenKind::Minus, .. },
        "*" => Token { kind: TokenKind::Star, .. },
        "+=" => Token { kind: TokenKind::PlusEq, .. },
        "-=" => Token { kind: TokenKind::MinusEq, .. },
        "*=" => Token { kind: TokenKind::StarEq, .. },
        "!" => Token { kind: TokenKind::Exclamation, .. },
        "!=" => Token { kind: TokenKind::ExclamationEq, .. },
        "&" => Token { kind: TokenKind::Amp, .. },
        "&&" => Token { kind: TokenKind::AmpAmp, .. },
        "|" => Token { kind: TokenKind::Pipe, .. },
        "||" => Token { kind: TokenKind::PipePipe, .. },
        "<" => Token { kind: TokenKind::LAngle, .. },
        "<<" => Token { kind: TokenKind::DoubleLAngle, .. },
        "<=" => Token { kind: TokenKind::LAngleEq, .. },
        ">" => Token { kind: TokenKind::RAngle, .. },
        ">>" => Token { kind: TokenKind::DoubleRAngle, .. },
        ">=" => Token { kind: TokenKind::RAngleEq, .. },
        "/" => Token { kind: TokenKind::Slash, .. },
        INDENT => Token { kind: TokenKind::Indent, .. },
        DEDENT => Token { kind: TokenKind::Dedent, .. },
        NEWLINE => Token { kind: TokenKind::Newline, .. },
        "as" => Token { kind: TokenKind::As, .. },
        "break" => Token { kind: TokenKind::Break, .. },
        "continue" => Token { kind: TokenKind::Continue, .. },
        "type" => Token { kind: TokenKind::Type, .. },
        "fn" => Token { kind: TokenKind::Fn, .. },
        "Fn" => Token { kind: TokenKind::UpperFn, .. },
        "let" => Token { kind: TokenKind::Let, .. },
        "if" => Token { kind: TokenKind::If, .. },
        "else" => Token { kind: TokenKind::Else, .. },
        "elif" => Token { kind: TokenKind::Elif, .. },
        "match" => Token { kind: TokenKind::Match, .. },
        "self" => Token { kind: TokenKind::Self_, .. },
        "for" => Token { kind: TokenKind::For, .. },
        "while" => Token { kind: TokenKind::While, .. },
        "in" => Token { kind: TokenKind::In, .. },
        "return" => Token { kind: TokenKind::Return, .. },
        "import" => Token { kind: TokenKind::Import, .. },
        "prim" => Token { kind: TokenKind::Prim, .. },
        "trait" => Token { kind: TokenKind::Trait, .. },
        "impl" => Token { kind: TokenKind::Impl, .. },
        IntLit => Token { kind: TokenKind::Int { .. }, .. },
        HexIntLit => Token { kind: TokenKind::HexInt { .. }, .. },
        BinIntLit => Token { kind: TokenKind::BinInt { .. }, .. },
        StringLit => Token { kind: TokenKind::String, .. },
        CharLit => Token { kind: TokenKind::Char, .. },
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Utils
//
////////////////////////////////////////////////////////////////////////////////////////////////////

// Note: This allows trailing separator.
Sep<T, S>: Vec<T> = {
    <mut v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// Note: This doesn't allow trailing separator.
Sep1<T, S>: Vec<T> = {
    <e0:T> <mut v:(S <T>)*> => {
        v.insert(0, e0);
        v
    }
};

LUpperId: L<Id> = {
    <l:@L> <id:UpperId> <r:@R> => L::new(module, l, r, id.smol_str()),
}

LLowerId: L<Id> = {
    <l:@L> <id:LowerId> <r:@R> => L::new(module, l, r, id.smol_str()),
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Type declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

TypeDecl: L<TypeDecl> = {
    <l:@L> "type" <type_name:UpperId> <type_params:TypeParams> <rhs:TypeDeclRhs> <r:@R> =>
        L::new(module, l, r, TypeDecl {
            name: type_name.smol_str(),
            type_params,
            rhs: Some(rhs),
        }),

    <l:@L> "prim"? "type" <type_name:UpperId> <type_params:TypeParams> NEWLINE <r:@R> =>
        L::new(module, l, r, TypeDecl {
            name: type_name.smol_str(),
            type_params,
            rhs: None,
        }),
}

TypeDeclRhs: TypeDeclRhs = {
    ":" NEWLINE INDENT <constrs:TypeConstrs> DEDENT =>
        TypeDeclRhs::Sum(constrs),

    ":" NEWLINE INDENT <named_fields:NamedFields> DEDENT =>
        TypeDeclRhs::Product(ConstructorFields::Named(named_fields)),
}

TypeParams: Vec<Id> = {
    => vec![],

    "[" <params:Sep<UpperId, ",">> "]" =>
        params.into_iter().map(|id| id.smol_str()).collect(),
}

TypeConstrs: Vec<ConstructorDecl> = {
    ConstructorDecl+
}

ConstructorDecl: ConstructorDecl = {
    <name:UpperId> NEWLINE =>
        ConstructorDecl {
            name: name.smol_str(),
            fields: ConstructorFields::Empty
        },

    <name:UpperId> ":" NEWLINE INDENT <named_fields:NamedFields> DEDENT =>
        ConstructorDecl {
            name: name.smol_str(),
            fields: ConstructorFields::Named(named_fields),
        },

    <name:UpperId> "(" <fields:UnnamedFields> ","? ")" NEWLINE =>
        ConstructorDecl {
            name: name.smol_str(),
            fields: ConstructorFields::Unnamed(fields),
        },
}

NamedFields: Vec<(Id, Type)> = {
    (<NamedField> NEWLINE)+
}

NamedField: (Id, Type) = {
    <name:LowerId> ":" <type_:Type> =>
        (name.smol_str(), type_),
}

UnnamedFields: Vec<Type> = {
    <mut fields:UnnamedFields> "," <field:Type> => {
        fields.push(field);
        fields
    },

    <field:Type> => vec![field],
}

LType: L<Type> = {
    <l:@L> <ty:Type> <r:@R> => L::new(module, l, r, ty),
}

LTypeNamed: L<Type> = {
    <l:@L> <ty:TypeNamed> <r:@R> => L::new(module, l, r, ty),
}

Type: Type = {
    <ty:TypeNamed> => ty,

    "Fn" "(" <args:Sep<LType, ",">> ")" <ret:(":" <LType>)?> =>
        Type::Fn(FnType { args, ret: ret.map(|t| t.map(|t| Box::new(t))) }),

    "(" <fields:Sep<RecordTypeField, ",">> <extension:RowExtension> ")" =>
        Type::Record { fields, extension },

    "[" <alts:Sep<VariantAlt, ",">> <extension:RowExtension> "]" =>
        Type::Variant { alts, extension, },
}

TypeNamed: Type = {
    <name:UpperId> =>
        Type::Named(NamedType { name: name.smol_str(), args: vec![] }),

    <name:UpperId> "[" <args:Sep<TypeArg, ",">> "]" =>
        Type::Named(NamedType { name: name.smol_str(), args }),
}

RowExtension: Option<Id> = {
    => None,

    ".." <id:UpperId> => Some(id.smol_str()),
}

TypeArg: L<(Option<Id>, L<Type>)> = {
    <l:@L> <name:UpperId> "=" <ty:LType> <r:@R> =>
        L::new(module, l, r, (Some(name.smol_str()), ty)),

    <ty:LType> => {
        let loc = ty.loc.clone();
        L { loc, node: (None, ty) }
    },
}

RecordTypeField: Named<Type> = {
    <name:LowerId> ":" <ty:Type> => Named { name: Some(name.smol_str()), node: ty },
    <ty:Type> => Named { name: None, node: ty },
}

VariantAlt: VariantAlt = {
    <name:UpperId> "(" <fields:Sep<RecordTypeField, ",">> ")" =>
        VariantAlt { con: name.smol_str(), fields },
    <name:UpperId> =>
        VariantAlt { con: name.smol_str(), fields: vec![] },
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Function declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

FunDecl: L<FunDecl> = {
    // We need two productions here as with two optional bracketed type/pred
    // list we can't tell that one list means only type list.

    // TODO: comma parsing after `self` is not right.

    <l:@L> <sig:FunSig> "=" NEWLINE INDENT <body_l:@L> <body:LStmts> <body_r:@R> DEDENT <r:@R> =>
        L::new(module, l, r, FunDecl {
            sig,
            body: Some(L::new(module, body_l, body_r, body)),
        }),

    <l:@L> "prim"? <sig:FunSig> NEWLINE <r:@R> =>
        L::new(module, l, r, FunDecl {
            sig,
            body: None,
        }),

    <l:@L> <sig:FunSig> "=" <rhs:LInlineExpr> NEWLINE <r:@R> => {
        let L { loc, node } = rhs;
        L::new(module, l, r, FunDecl {
            sig,
            body: Some(L {
                loc: loc.clone(),
                node:  vec![L { loc: loc.clone(), node: Stmt::Expr(L { loc: loc.clone(), node: node }) }],
            })
        })
    },
}

LFunSig: L<FunSig> = {
    <l:@L> <sig:FunSig> <r:@R> =>
        L::new(module, l, r, sig),
}

FunSig: FunSig = {
    "fn" <name:LLowerId>
            <type_params:Context>
            "(" <self_:("self" ","?)?> <params:Sep<(<LowerId> ":" <LType>), ",">> ")"
            <return_ty:(":" <LType>)?> =>
        FunSig {
            name,
            type_params,
            self_: self_.is_some(),
            params: params.into_iter().map(|(name, ty)| (name.smol_str(), ty)).collect(),
            return_ty,
        },
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Statements
//
////////////////////////////////////////////////////////////////////////////////////////////////////

LStmts: Vec<L<Stmt>> = {
    <stmts:LStmt*> =>
        stmts,
}

pub LStmt: L<Stmt> = {
    <l:@L> <stmt:Stmt> <r:@R> => L::new(module, l, r, stmt),
}

Stmt: Stmt = {
    "break" NEWLINE => Stmt::Break,

    "continue" NEWLINE => Stmt::Continue,

    "let" <lhs:LPat> <ty:(":" <LType>)?> "=" <rhs:LInlineExpr> NEWLINE =>
        Stmt::Let(LetStmt { lhs, ty, rhs }),

    "let" <lhs:LPat> <ty:(":" <LType>)?> "=" <rhs:LBlockExpr> =>
        Stmt::Let(LetStmt { lhs, ty, rhs }),

    <lhs:LInlineExpr> <op:AssignOp> <rhs:LExpr> NEWLINE =>
        Stmt::Assign(AssignStmt {
            lhs,
            rhs,
            op,
        }),

    <l:@L> <expr:InlineExpr> <r:@R> NEWLINE =>
        Stmt::Expr(L::new(module, l, r, expr)),

    <l:@L> <expr:BlockExpr> <r:@R> =>
        Stmt::Expr(L::new(module, l, r, expr)),

    "for" <id:LowerId> "in" <expr:LExpr> ":" NEWLINE INDENT <statements:LStmts> DEDENT =>
        Stmt::For(ForStmt {
            var: id.smol_str(),
            ty: None,
            expr,
            expr_ty: None,
            body: statements,
        }),

    "while" <cond:LExpr> ":" NEWLINE INDENT <statements:LStmts> DEDENT =>
        Stmt::While(WhileStmt {
            cond,
            body:statements,
        }),

    // <fun:FunDecl> =>
    //     Stmt::LetFn(fun),
}

Alts: Vec<Alt> = {
    Alt*
}

Alt: Alt = {
    <pattern:LPat> ":" NEWLINE INDENT <stmts:LStmts> DEDENT =>
        Alt {
            pattern,
            guard: None,
            rhs: stmts,
        },

    <pattern:LPat> ":" <stmt:LStmt>  =>
        Alt {
            pattern,
            guard: None,
            rhs: vec![stmt],
        },
}

AssignOp: AssignOp = {
    "=" => AssignOp::Eq,
    "+=" => AssignOp::PlusEq,
    "-=" => AssignOp::MinusEq,
    "*=" => AssignOp::StarEq,
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Expressions
//
////////////////////////////////////////////////////////////////////////////////////////////////////

pub LExpr: L<Expr> = {
    <l:@L> <expr:Expr> <r:@R> => L::new(module, l, r, expr),
}

Expr: Expr = {
    <expr:InlineExpr> => expr,
    <expr:BlockExpr>  => expr,
}

LBlockExpr: L<Expr> = {
    <l:@L> <expr:BlockExpr> <r:@R> =>
        L::new(module, l, r, expr),
}

// Block expressions are also statements, without a NEWLINE after them.
BlockExpr: Expr = {
    "match" <expr:LInlineExpr> ":" NEWLINE INDENT <alts:Alts> DEDENT =>
        Expr::Match(MatchExpr {
            scrutinee: Box::new(expr),
            alts,
        }),

    "if" <cond:LExpr> ":" NEWLINE INDENT <then_:LStmts> DEDENT
            <mut branches:("elif" <LExpr> ":" NEWLINE INDENT <LStmts> DEDENT)*>
            <else_branch:("else" ":" NEWLINE INDENT <LStmts> DEDENT)?> => {
        branches.insert(0, (cond, then_));
        Expr::If(IfExpr {
            branches,
            else_branch,
        })
    },
}

LInlineExpr: L<Expr> = {
    <l:@L> <expr:InlineExpr> <r:@R> =>
        L::new(module, l, r, expr),
}

// Inline expressions can be made statements with a NEWLINE after them.
InlineExpr: Expr = {
    #[precedence(level = "0")]
    "self" =>
        Expr::Self_,

    <id:LowerId> =>
        Expr::Var(VarExpr { id: id.smol_str(), ty_args: vec![] }),

    <id:UpperId> =>
        Expr::Constr(ConstrExpr { id: id.smol_str(), ty_args: vec![] }),

    "(" <mut exprs:Sep<ParenExpr, ",">> ")" => {
        match exprs.len() {
            0 => Expr::Record(vec![]),
            1 if exprs[0].0.is_none() => exprs.pop().unwrap().1.node,
            _ => Expr::Record(exprs.into_iter().map(|(name, expr)| Named { name: name, node: expr }).collect()),
        }
    },

    <int:IntLit> => {
        let kind = match int.kind {
            TokenKind::Int(int_kind) => int_kind,
            _ => unreachable!(),
        };
        let token_str = &int.text;

        // TODO: Allocate the buffer once and reuse.
        // Even better, build the number as we scan for underscores.
        let mut int_str = String::new();

        // Drop "i8", "u32" etc. suffix.
        let suffix = match kind {
            Some(IntKind::I8) => Some("i8"),
            Some(IntKind::U8) => Some("u8"),
            Some(IntKind::I32) => Some("i32"),
            Some(IntKind::U32) => Some("u32"),
            None => None,
        };

        let token_str = match suffix {
            None => token_str,
            Some(suffix) => token_str.strip_suffix(suffix).unwrap(),
        };

        // Drop '_'s.
        for char in token_str.chars() {
            if char == '_' {
                continue;
            }
            int_str.push(char);
        }

        Expr::Int(IntExpr {
            text: int_str,
            suffix: kind,
            radix: 10,
            parsed: 0,
        })
    },

    <int:HexIntLit> => {
        let kind = match int.kind {
            TokenKind::HexInt(int_kind) => int_kind,
            _ => unreachable!(),
        };
        debug_assert!(int.text.starts_with("0x"));
        let token_str = &int.text["0x".len()..];

        // TODO: Allocate the buffer once and reuse.
        // Even better, build the number as we scan for underscores.
        let mut int_str = String::new();

        // Drop "i8", "u32" etc. suffix.
        let suffix = match kind {
            Some(IntKind::I8) => Some("i8"),
            Some(IntKind::U8) => Some("u8"),
            Some(IntKind::I32) => Some("i32"),
            Some(IntKind::U32) => Some("u32"),
            None => None,
        };

        let token_str = match suffix {
            None => token_str,
            Some(suffix) => token_str.strip_suffix(suffix).unwrap(),
        };

        // Drop '_'s.
        for char in token_str.chars() {
            if char == '_' {
                continue;
            }
            int_str.push(char);
        }

        Expr::Int(IntExpr {
            text: int_str,
            suffix: kind,
            radix: 16,
            parsed: 0,
        })
    },

    <int:BinIntLit> => {
        let kind = match int.kind {
            TokenKind::BinInt(int_kind) => int_kind,
            _ => unreachable!(),
        };
        debug_assert!(int.text.starts_with("0b"));
        let token_str = &int.text["0x".len()..];

        // TODO: Allocate the buffer once and reuse.
        // Even better, build the number as we scan for underscores.
        let mut int_str = String::new();

        // Drop "i8", "u32" etc. suffix.
        let suffix = match kind {
            Some(IntKind::I8) => Some("i8"),
            Some(IntKind::U8) => Some("u8"),
            Some(IntKind::I32) => Some("i32"),
            Some(IntKind::U32) => Some("u32"),
            None => None,
        };

        let token_str = match suffix {
            None => token_str,
            Some(suffix) => token_str.strip_suffix(suffix).unwrap(),
        };

        // Drop '_'s.
        for char in token_str.chars() {
            if char == '_' {
                continue;
            }
            int_str.push(char);
        }

        Expr::Int(IntExpr {
            text: int_str,
            suffix: kind,
            radix: 2,
            parsed: 0,
        })
    },

    <l:@L> <string:StringLit> => {
        let l = Loc { line: l.line, col: l.col + 1, byte_idx: l.byte_idx + 1 };
        Expr::String(parse_string_parts(module, &string.text[1..string.text.len() - 1], l))
    },

    <char:CharLit> => Expr::Char(parse_char_lit(&char.text)),

    <l:@L> <fun:InlineExpr> <r:@R> "(" <args:Sep<CallArg, ",">> ")" =>
        Expr::Call(CallExpr {
            fun: Box::new(L::new(module, l, r, fun)),
            args,
        }),

    <l:@L> <expr:InlineExpr> <r:@R> "." <field:LowerId> =>
        match expr {
            Expr::Constr(ConstrExpr { id, ty_args: _ }) =>
                Expr::AssocFnSelect(AssocFnSelectExpr { ty: id, member: field.smol_str(), ty_args: vec![] }),

            _ => Expr::FieldSelect(FieldSelectExpr {
                object: Box::new(L::new(module, l, r, expr)),
                field: field.smol_str(),
            }),
        },

    <expr:InlineExpr> "." <constr:UpperId> => {
        match expr {
            Expr::Constr(ConstrExpr { id: ty, ty_args: _ }) => Expr::ConstrSelect(ConstrSelectExpr {
                ty,
                constr: constr.smol_str(),
                ty_args: vec![],
            }),

            _ => panic!(),
        }
    },

    #[precedence(level = "2")]
    "!" <l:@L> <expr:InlineExpr> <r:@R> =>
        Expr::UnOp(UnOpExpr { op: UnOp::Not, expr: Box::new(L::new(module, l, r, expr)) }),

    #[precedence(level = "3")]
    <id:BacktickUpperId> <args:("(" <Sep<LExpr, ",">> ")")?> =>
        Expr::Variant(VariantExpr {
            id: id.text.as_str()[1..].into(),
            args: args.unwrap_or(vec![]),
        }),

    #[precedence(level = "4")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "*" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Multiply,
        }),

    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "/" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Divide,
        }),

    #[precedence(level = "5")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "+" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Add,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> "-" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Subtract,
        }),

    #[precedence(level = "6")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "<<" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::LeftShift,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> ">>" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::RightShift,
        }),

    #[precedence(level = "7")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "&" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::BitAnd,
        }),

    #[precedence(level = "8")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "|" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::BitOr,
        }),

    #[precedence(level = "9")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "==" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Equal,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> "!=" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::NotEqual,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> "<" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Lt,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> ">" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Gt,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> "<=" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::LtEq,
        }),

    <l1:@L> <left:InlineExpr> <r1:@R> ">=" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::GtEq,
        }),

    #[precedence(level = "10")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "&&" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::And,
        }),

    #[precedence(level = "11")]
    #[assoc(side = "left")]
    <l1:@L> <left:InlineExpr> <r1:@R> "||" <l2:@L> <right:InlineExpr> <r2:@R> =>
        Expr::BinOp(BinOpExpr {
            left: Box::new(L::new(module, l1, r1, left)),
            right: Box::new(L::new(module, l2, r2, right)),
            op: BinOp::Or,
        }),

    #[precedence(level = "12")]
    #[assoc(side = "left")]
    <l1:@L> <from:InlineExpr> <r1:@R> ".." <l2:@L> <to:InlineExpr> <r2:@R> =>
        Expr::Range(RangeExpr {
            from: Box::new(L::new(module, l1, r1, from)),
            to: Box::new(L::new(module, l2, r2, to)),
            inclusive: false,
        }),

    <l1:@L> <from:InlineExpr> <r1:@R> "..=" <l2:@L> <to:InlineExpr> <r2:@R> =>
        Expr::Range(RangeExpr {
            from: Box::new(L::new(module, l1, r1, from)),
            to: Box::new(L::new(module, l2, r2, to)),
            inclusive: true,
        }),

    #[precedence(level = "13")]
    "return" <expr:LInlineExpr> =>
        Expr::Return(Box::new(expr)),
}

ParenExpr: (Option<Id>, L<Expr>) = {
    <id:LowerId> "=" <expr:LExpr> =>
        (Some(id.smol_str()), expr),

    <expr:LExpr> =>
        (None, expr),
}

CallArg: CallArg = {
    <name:LowerId> "=" <expr:LExpr> =>
        CallArg {
            name: Some(name.smol_str()),
            expr,
        },

    <expr:LExpr> =>
        CallArg {
            name: None,
            expr,
        },
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Patterns
//
////////////////////////////////////////////////////////////////////////////////////////////////////

LPat: L<Pat> = {
    <l:@L> <pat:Pat> <r:@R> => L::new(module, l, r, pat),
}

Pat: Pat = {
    #[precedence(level = "0")]
    <id:LowerId> => Pat::Var(id.smol_str()),

    <constr:ConstrPattern> => Pat::Constr(constr),

    <var:VariantPattern> => Pat::Variant(var),

    "(" <fields:Sep<PatternField, ",">> ")" =>
        Pat::Record(fields.into_iter().map(|(name, pat)| Named { name, node: pat }).collect()),

    "_" => Pat::Ignore,

    <str:StringLit> => Pat::Str(str.text[1..str.text.len() - 1].to_string()),

    <char:CharLit> => Pat::Char(parse_char_lit(&char.text)),

    <str:StringLit> <id:LowerId> => Pat::StrPfx(str.text[1..str.text.len() - 1].to_string(), id.smol_str()),

    #[precedence(level = "1")]
    #[assoc(side = "right")]
    <l1:@L> <p1:Pat> <r1:@R> "|" <l2:@L> <p2:Pat> <r2:@L> => {
        Pat::Or(
            Box::new(L::new(module, l1, r1, p1)),
            Box::new(L::new(module, l2, r2, p2)),
        )
    }
}

Constructor: Constructor = {
    <type_:UpperId> "." <constr:UpperId> =>
        Constructor { type_: type_.smol_str(), constr: Some(constr.smol_str()) },

    <type_:UpperId> =>
        Constructor { type_: type_.smol_str(), constr: None },
}

ConstrPattern: ConstrPattern = {
    <constr:Constructor> =>
        ConstrPattern { constr, fields: vec![], ty_args: vec![] },

    <constr:Constructor> "(" <fields:Sep<PatternField, ",">> ")" =>
        ConstrPattern {
            constr,
            fields: fields.into_iter().map(|(name, pat)| Named { name, node: pat }).collect(),
            ty_args: vec![],
        },
}

VariantPattern: VariantPattern = {
    <constr:BacktickUpperId> =>
        VariantPattern {
            constr: constr.text.as_str()[1..].into(),
            fields: vec![],
        },

    <constr:BacktickUpperId> "(" <fields:Sep<PatternField, ",">> ")" =>
        VariantPattern {
            constr: constr.text.as_str()[1..].into(),
            fields: fields.into_iter().map(|(name, pat)| Named { name, node: pat }).collect(),
        },
}

PatternField: (Option<Id>, L<Pat>) = {
    <id:LowerId> "=" <pat:LPat> =>
        (Some(id.smol_str()), pat),

    <pat:LPat> =>
        (None, pat),
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Import declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

ImportDecl: L<ImportDecl> = {
    <l:@L> "import" <path:Sep<UpperId, ".">> NEWLINE <r:@R> =>
        L::new(module, l, r, ImportDecl { path: path.into_iter().map(|t| t.smol_str()).collect() }),
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Trait declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

TraitDecl: L<TraitDecl> = {
    <l:@L> "trait" <name:LUpperId> "[" <bound_l:@L> <ty:UpperId> <bounds:(":" <Sep<LType, "+">>)?> <bound_r:@R> "]" ":"
            NEWLINE INDENT <items:TraitDeclItem+> DEDENT <r:@R> =>
        L::new(module, l, r, TraitDecl {
            name,
            ty: L::new(module, bound_l, bound_r, (ty.smol_str(), bounds.unwrap_or(vec![]))),
            items,
        })
}

TraitDeclItem: L<TraitDeclItem> = {
    <l:@L> "type" <name:UpperId> NEWLINE <r:@R> =>
        L::new(module, l, r, TraitDeclItem::AssocTy(name.smol_str())),

    <fun_decl:FunDecl> =>
         fun_decl.map(TraitDeclItem::Fun),
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Impl declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

ImplDecl: L<ImplDecl> = {
    <l:@L> "impl" <context:Context> <trait_:(<LUpperId> "for")?> <ty:LTypeNamed> ":"
            NEWLINE INDENT <items:ImplDeclItem*> DEDENT <r:@R> => {
        L::new(module, l, r, ImplDecl {
            context,
            trait_,
            ty,
            items,
        })
    }
}

//  type Context = Vec<L<(L<Id>, Vec<L<Type>>)>>;
Context: Context = {
    => vec![],

    "[" <tys:Sep<TypeWithBounds, ",">> "]" => tys,
}

TypeWithBounds: L<(L<Id>, Vec<L<Type>>)> = {
    <l:@L> <ty_param:LUpperId> <r:@R> => L::new(module, l, r, (ty_param, vec![])),
    <l:@L> <ty_param:LUpperId> ":" <tys:Sep<LType, "+">> <r:@R> => L::new(module, l, r, (ty_param, tys)),
}

ImplDeclItem: L<ImplDeclItem> = {
    <l:@L> "type" <name:UpperId> "=" <ty:LType> NEWLINE <r:@R> =>
        L::new(module, l, r, ImplDeclItem::AssocTy(AssocTyDecl { name: name.smol_str(), ty })),

    <fun_decl:FunDecl> =>
        fun_decl.map(ImplDeclItem::Fun),
}

////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Top-level declarations
//
////////////////////////////////////////////////////////////////////////////////////////////////////

pub TopDecls: Vec<L<TopDecl>> = {
    TopDecl*
}

TopDecl: L<TopDecl> = {
    NEWLINE* <l:@L> <type_decl:TypeDecl> <r:@R> =>
        L::new(module, l, r, TopDecl::Type(type_decl)),

    NEWLINE* <l:@L> <fun_decl:FunDecl> <r:@R> =>
        L::new(module, l, r, TopDecl::Fun(fun_decl)),

    NEWLINE* <l:@L> <import_decl:ImportDecl> <r:@R> =>
        L::new(module, l, r, TopDecl::Import(import_decl)),

    NEWLINE* <l:@L> <trait_decl:TraitDecl> <r:@R> =>
        L::new(module, l, r, TopDecl::Trait(trait_decl)),

    NEWLINE* <l:@L> <impl_decl:ImplDecl> <r:@R> =>
        L::new(module, l, r, TopDecl::Impl(impl_decl)),
}
